Language "Golang"

[
  #1 Lexical
  #2 Types
  #3 Expressions
  #4 Statements
  #5 Declarations
  #A Disambiguation
]

Syntax
  START : start ::= sourceFile

Semantics
  start[[START:start]] : =>integers

Rule
  start[[SourceFile]]
    = initialise-giving
      initialise-binding
      initialise-storing
      scope(
        {
          // Types:
          "any"    |-> values,
          "bool"   |-> bools,
          "int"    |-> int,
          "int8"   |-> int8,
          "int16"  |-> int16,
          "int32"  |-> int32,
          "int64"  |-> int64,
          "uint"   |-> uint,
          "uint8"  |-> uint8,
          "uint16" |-> uint16,
          "uint32" |-> uint32,
          "uint64" |-> uint64,
          // Constants:
          "true"   |-> true,
          "false"  |-> false,
          // Zero value:
          "nil"    |-> null,
          "print"  |-> function closure print(tuple-elements given)
        },
        handle-abrupt(sequential(multithread execute-sourcefile[[SourceFile]], "succes"), "failure"))

Syntax
  SourceFile        : sourceFile        ::= 'package' identifier ';' topLevelDeclSemic*
  TopLevelDeclSemic : topLevelDeclSemic ::= topLevelDecl ';'

Semantics
  execute-sourcefile[[_:sourceFile]] : =>null-value

Rule
  execute-sourcefile[['package' 'main' ';' TopLevelDeclSemic*]]
    = scope(
        allocate-topleveldeclsemic[[TopLevelDeclSemic*]],
        sequential(
          initialise-topleveldeclsemic[[TopLevelDeclSemic*]],
          apply(assigned bound "main", tuple())))

Otherwise
  execute-sourcefile[['package' Identifier ';' TopLevelDeclSemic*]]
    = scope(
        allocate-topleveldeclsemic[[TopLevelDeclSemic*]],
        initialise-topleveldeclsemic[[TopLevelDeclSemic*]])

Semantics
  allocate-topleveldeclsemic[[_:topLevelDeclSemic*]] : =>envs

Rule
  allocate-topleveldeclsemic[[ ]] = map( )

Rule
  allocate-topleveldeclsemic[[TopLevelDecl ';' TopLevelDeclSemic*]]
    = collateral(
        allocate-topleveldecl[[TopLevelDecl]],
        allocate-topleveldeclsemic[[TopLevelDeclSemic*]])

Semantics
  initialise-topleveldeclsemic[[_:topLevelDeclSemic*]] : =>null-value

Rule
  initialise-topleveldeclsemic[[ ]] = null-value

Rule
  initialise-topleveldeclsemic[[TopLevelDecl ';' TopLevelDeclSemic*]]
    = sequential(
        initialise-topleveldecl[[TopLevelDecl]],
        initialise-topleveldeclsemic[[TopLevelDeclSemic*]])





